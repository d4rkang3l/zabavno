#!/usr/bin/env scheme-script
;; -*- mode: scheme; coding: utf-8 -*- !#
;; PC emulator in Scheme
;; Copyright © 2016 Göran Weinholt <goran@weinholt.se>

;; Permission is hereby granted, free of charge, to any person obtaining a
;; copy of this software and associated documentation files (the "Software"),
;; to deal in the Software without restriction, including without limitation
;; the rights to use, copy, modify, merge, publish, distribute, sublicense,
;; and/or sell copies of the Software, and to permit persons to whom the
;; Software is furnished to do so, subject to the following conditions:

;; The above copyright notice and this permission notice shall be included in
;; all copies or substantial portions of the Software.

;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.
#!r6rs

(import (rnrs (6))
        (zabavno cpu x86)
        (zabavno firmware pcbios)
        (zabavno loader pcboot)
        (zabavno loader dos))

(define (main filename program-args enable-debug enable-trace)
  (let ((M (make-machine)))
    (with-machine M
      (lambda ()
        (machine-debug-set! M enable-debug)
        (machine-trace-set! M enable-trace)
        (call-with-port (open-file-input-port filename)
          (lambda (p)
            (let ((bios-data (pcbios-setup)))
              (cond ((detect-boot-sector p)
                     (load-boot-sector p)
                     (pcbios-load-floppy-image bios-data 0 p)) ;drive A
                    ((detect-dos-exe-image p)
                     (load-dos-exe-image p program-args))
                    (else
                     (load-dos-com-image p program-args)))
              (let lp ()
                (machine-run)                 ;run until HLT
                (when (eq? (pcbios-post-emulator-exit bios-data M) 'continue-emulator)
                  (lp))))))))))

(define (help)
  (display "The zabavno machine emulator test program\n\
Usage: zabavno [--debug] [--trace] [--args <program-args...>] <program-image>\n\
The image may be a DOS .com file, DOS .exe file or a bootable PC floppy image.\n"
           (current-error-port))
  (exit 1))

;; Handle command line arguments.
(let lp ((parameters (cdr (command-line)))
         (filename #f)
         (program-args "")
         (enable-debug #f)
         (enable-trace #f))
  (cond ((null? parameters)
         (when (not filename)
           (help))
         (main filename program-args enable-debug enable-trace))
        ((equal? (car parameters) "--debug")
         (lp (cdr parameters) filename program-args #t enable-trace))
        ((equal? (car parameters) "--trace")
         (lp (cdr parameters) filename program-args enable-debug #t))
        ((equal? (car parameters) "--args")
         (when (null? (cdr parameters))
           (help))
         (lp (cddr parameters) filename (cadr parameters) enable-debug enable-trace))
        ((not filename)
         (lp (cdr parameters) (car parameters) program-args enable-debug enable-trace))
        (else
         (help))))
